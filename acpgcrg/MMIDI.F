      SUBROUTINE MMIDI(VKL,VNE,T,WVL,AVI,SMT,VLI,DVIDL,NV,ZCURR,HTOT,
     $NSTEP,VLIO,SEPCM)
C     Subroutine for numerical integration by the modified midpoint 
C     method. See Numerical Recipes by Press et al., p.560.
	IMPLICIT DOUBLE PRECISION (A-H,O-Z)
	PARAMETER (NMAX=10)
	DIMENSION VLI(NV),DVIDL(NV),VLIO(NV),YM(NMAX),YN(NMAX),DVIOUT(NV)
	H=HTOT/NSTEP
	DO 11 I=1,NV
		YM(I)=VLI(I)
		YN(I)=VLI(I)+H*DVIDL(I)
   11 CONTINUE
	Z=ZCURR*SEPCM+H
	CALL DERIV(VKL,VNE,T,WVL,AVI,SMT,YN,DVIOUT)
	H2=2.D0*H
	DO 13 N=2,NSTEP
		DO 12 I=1,NV
			SWAP=YM(I)+H2*DVIOUT(I)
			YM(I)=YN(I)
			YN(I)=SWAP
   12		CONTINUE
		Z=Z+H
		CALL DERIV(VKL,VNE,T,WVL,AVI,SMT,YN,DVIOUT)
   13 CONTINUE
      DO 14 I=1,NV
		VLIO(I)=0.5D0*(YM(I)+YN(I)+H*DVIOUT(I))
   14 CONTINUE
	ZCURR=Z/SEPCM
	RETURN
	END