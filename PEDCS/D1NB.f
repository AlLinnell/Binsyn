      SUBROUTINE D1NB(X,AR1,AR2,AR3,JL,NL,KL,U,DV)  
C     VERSION OF 8/11/83
C     SRT TO CALCULATE THE FIRST DERIVATIVE OF A TABULAR FUNCTION BY     
C     NEWTON,S SECOND METHOD   
C     SUCCESSIVE TESTS ARE MADE TO MAKE SURE THERE ARE SUFFICIENT 
C     TABULAR ENTRIES TO PERMIT CALCULATION OF THE REQUIRED NUMBER OF    
C     DIFFERENCES
C     AR1 IS THE ARRAY OF INDEPENDENT VARIABLE VALUES,EQUALLY SPACED     
C     AR2 IS THE ARRAY OF DEPENDENT VARIABLE VALUES 
C     AR3 IS THE ARRAY OF HORIZONTAL DIFFERENCES CALCULATED BY SRT DFB   
C     JL IS THE LENGTH OF ARRAYS AR1 AND AR2 
C     NL IS THE LENGTH OF ARRAY AR3   
C     KL IS THE NUMBER OF DIFFERENCES CALCULATED IN SRT DFB
C     U IS THE FRACTIONAL DISTANCE OF THE ABSCISSA FROM THE LAST  
C     PREVIOUS TABULAR VALUE,MEASURED IN UNITS OF ABSCISSA SPACING
C     DV IS THE CALCULATED VALUE OF THE DERIVATIVE RETURNED BY THIS SRT  
C     NOTE THAT SRT DFB MUST BE CALLED BEFORE THIS SRT     
C     NOTE THAT NO CALL FOR MORE THAN SIXTH ORDER DIFFERENCES IS ALLOWED 
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION AR1(JL),AR2(JL),AR3(NL)      
      H=AR1(2)-AR1(1)   
      DV=0.0     
      DO 1 J=1,JL
      U=(X-AR1(J))/H    
      IF (U.LE.0.0) GO TO 2    
    1 CONTINUE   
      CALL PMDSTOP      
    2 IF (J.EQ.1) CALL PMDSTOP 
    5 IF (KL.LT.1) CALL PMDSTOP
      IF (J.LT.2) CALL PMDSTOP 
      DV=AR3(J)  
      IF (KL.LE.1) GO TO 10    
      IF (J.LT.3) CALL PMDSTOP 
      DV=DV+(2.0*U+1.0)*AR3(J+JL)/2.0 
      IF (KL.EQ.2) GO TO 10    
      IF (J.LT.4) CALL PMDSTOP 
      DV=DV+((3.0*U+6.0)*U+2.0)*AR3(J+2*JL)/6.0     
      IF (KL.EQ.3) GO TO 10    
      IF (J.LT.5) CALL PMDSTOP 
      DV=DV+(((4.0*U+18.0)*U+22.0)*U+6.0)*AR3(J+3*JL)/24.0 
      IF (KL.EQ.4) GO TO 10    
      IF (J.LT.6) CALL PMDSTOP 
      DV=DV+((((5.0*U+40.0)*U+105.0)*U+100.0)*U+24.0)*AR3(J+4*JL)/120.0  
      IF (KL.EQ.5) GO TO 10    
      IF (J.LT.7) CALL PMDSTOP 
      DV=DV+(((((6.0*U+75.0)*U+340.)*U+645.)*U+548.)*U+120.)*AR3(J+5*JL) 
     $/720.      
      IF (KL.GT.6) CALL PMDSTOP
   10 DV=DV/H    
   12 RETURN     
      END 
