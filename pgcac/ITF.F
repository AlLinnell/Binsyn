      SUBROUTINE ITF(ARY,NST,NND,H,YM1,YNP1,KY,ER,SM,MRY,KRT)  
C     SRT TO CHOOSE THE PROPER INTEGRATION RULE AND TO DO A NUMERICAL    
C     INTEGRATION
C     ITF WILL PERFORM THE INTEGRATION WHETHER THERE IS AN EVEN OR ODD
C     NUMBER OF SUBINTERVALS
C     IF THERE IS AN EVEN NUMBER, ITF USES SIMPSON'S RULE
C     IF THERE IS AN ODD NUMBER, ITF USES THE TRAPEZOIDAL RULE FOR
C     THE SINGLE SUBINTERVAL THAT MAKES THE SMALLEST CONTRIBUTION
C     TO THE INTEGRAL, AND COMPLETES THE INTEGRAL USING SIMPSON'S
C     RULE
C     ARY IS THE NAME OF THE ARRAY CONTAINING ORDINATES    
C     NST IS THE ORDINAL LOCATION TO START INTEGRATION IN ARY     
C     NND IS THE ORDINAL LOCATION OF THE FINAL ORDINATE TO USE IN ARY    
C     H IS THE INCREMENT
C     KY IS A CONTROL INTEGER.IF KY=0,DO NOT CALCULATE THE ERROR IN      
C     THE INTEGRATION,OTHERWISE YES   
C     ER IS THE CALCULATED ERROR,IF SPECIFIED
C     SM IS THE INTEGRATION VALUE 
C     YM1 IS THE ORDINATE IMMEDIATELY BEFORE THE NST ORDINATE
C     YNP1 IS THE ORDINATE IMMEDIATELY AFTER THE NND ORDINATE    
C     KRT IS AN INDICATOR OF ACCURACY FOR COMBINED INTEGRATIONS   
C     THE LARGER THE VALUE OF KRT,THE LOWER THE PROBABLE ACCURACY 
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION ARY(MRY)
	DATA ERLT/1.D-13/ 
      KRT=0      
      SM=0.D0
      ER=0.D0    
      IF (DABS(H).LE.ERLT) GO TO 99   
      N=NND-NST  
      IF (N.EQ.0) GO TO 99     
      KST=NST    
      KND=NND    
      K=MOD(N,2) 
C    IF K.EQ.0,N IS EVEN. USE SIMPSON'S RULE
      IF (K.EQ.0) GO TO 10
C     CANNOT USE SIMPSON'S RULE FOR FULL INTERVAL
C     LOCATE THE SINGLE SUBINTERVAL THAT MAKES THE SMALLEST CONTRIBUTION
C     TO THE INTEGRAL	     
      KRT=1      
      IF (DABS(ARY(KST)).GT.DABS(ARY(KND))) GO TO 5
C     THE INITIAL SUBINTERVAL MAKES THE SMALLEST CONTRIBUTION
C     DO TRAPEZOIDAL INTEGRATION OVER THIS SUBINTERVAL   
      SM=0.5D0*(ARY(KST)+ARY(KST+1))*H+SM      
      N=N-1      
      IF (N.EQ.0) GO TO 99
C     THERE IS A REMAINING INTEGRATION TO PERFORM, AND IT CAN BE DONE
C     VIA SIMPSON'S RULE	     
      KST=KST+1  
      GO TO 10
C     THE FINAL SUBINTERVAL MAKES THE SMALLEST CONTRIBUTION TO THE
C     INTEGRAL. INTEGRATE OVER IT WITH THE TRAPEZOIDAL RULE.	   
    5 SM=0.5D0*H*(ARY(KND-1)+ARY(KND))+SM      
      N=N-1      
      IF (N.EQ.0) GO TO 99     
      KND=KND-1
C     COMPLETE THE INTEGRATION VIA SIMPSON'S RULE	  
   10 CALL SMP(ARY,KST,KND,H,YM1,YNP1,KY,ER1,MRY,SM1)   
      ER=ER+ER1  
      SM=SM+SM1  
   99 RETURN     
      END 
