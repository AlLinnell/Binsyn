      SUBROUTINE POTN(I,ER,LM,NOUT1)
C     THIS SRT FINDS THE X AND Y VALUES FOR WHICH DZ/DX=0 AND     
C     STORES THEM IN XMN(I),YMN
C     THE VALUE OF Z=Z(MINIMUM) IS RETURNED IN THE CALLING PARAMETERS    
C     AND ALSO IS STORED IN COMMON IN ZMN    
C     THE SRT CALCULATES ALMX(I),WHERE ALMX(I) IS THE DIRECTION COSINE   
C     RELATIVE TO THE X-AXIS OF THE LINE TANGENT TO THE MINIMUM Z FOR    
C     Y=0 
C     THE SRT ALSO CALCULATES ALMY(I),WHERE ALMY(I) IS THE DIRECTION     
C     COSINE RELATIVE TO THE X-AXIS OF THE LINE TANGENT TO THE MINIMUM   
C     Y FOR Z=0  
C     NOTE THAT THE POINT OF MINIMUM Z ALWAYS IS CLOSER TO THE STAR OF   
C     SMALLER MASS THAN THE L1 POINT.THIS IS THE REASON FOR THE INITIAL  
C     LOOP FILLING ARRAY AR1   
C     NOTE THAT SOME OF THE COMMON ARRAYS OVERLAP ALM.THE OVERLAP WILL   
C     BE ERASED WHEN THE FINAL ALM VALUES ARE CALCULATED AND STORED      
C     N=MAXIMUM NUMBER OF ITERATIONS  
C  
C     THE L1 POINT IS A GOOD APPROXIMATION,INITIALLY,TO XMN(I)    
C     ITERATE UNTIL THE CHANGE IN  ZMN   IN TWO SUCCESSIVE ITERATIONS    
C     IS LESS THAN ER   
      INCLUDE 'COMA.FOR'
	PARAMETER (MNN    =    9)
      DIMENSION AR1(MNN),AR2(MNN),AR4(MTR)
	DATA ERLT/1.D-13/ 
      Y=0.D0
C     THE MINIMUM MUST OCCUR IN THE RANGE BETWEEN THE L1 POINT AND THE   
C     CENTER OF THE COMPANION  
      RG=1.D0-VL(I,1)    
      DLT=RG/20.D0
      ZMN=VL(I,1)
      DO 14 ICT=1,6     
      DO 3 J=1,9 
      LL=J-5     
      AR1(J)=XMN(I)+LL*DLT     
      X=AR1(J)   
C     FIND Z FOR ASSIGNED X AND Y     
      CALL PTN(I,X,Y,Z,ER,LM)  
      AR2(J)=Z   
    3 CONTINUE   
      CALL FIT(9,AR1,AR2,AR4,KR)      
      IF (KR.EQ.0) GO TO 4     
      IF ((KR.NE.129).OR.(ICT.LT.2)) CALL PMDSTOP   
  100 FORMAT (1X,'POTN',2I5)   
      WRITE (NOUT1,100) KR,ICT     
      ZMN=1000.D0
      DO 7 J=1,9 
      IF (AR2(J).LT.ZMN) ZMN=AR2(J)   
      IF (DABS(ZMN-AR2(J)).LE.ERLT) XMN(I)=AR1(J)
    7 CONTINUE   
      X=XMN(I)   
      Z=ZMN      
      GO TO 13   
    4 XMN(I)=-AR4(2)/AR4(3)/2.D0
      X=XMN(I)   
      CALL PTN(I,X,Y,Z,ER,LM)  
      ZMN=Z      
   13 DLT=DLT/3.D0
   14 CONTINUE   
    6 BKT1(I)=X*X+Y*Y+Z*Z      
      BKT1(3-I)=DSQRT(1.D0-2.D0*X+BKT1(I))      
      BKT1(I)=DSQRT(BKT1(I))    
      ALMX(I)=X/BKT1(I) 
      ALMX(3-I)=(1.D0-X)/BKT1(3-I)     
C     NOW FIND THE Y CORRES.TO XMN(I) AND Z=0
      Z=0.D0
      CALL PYTN(I,X,YMN,Z,ER,LM)      
      BKT1(I)=(X**2+YMN**2+Z**2)**0.5D0 
      BKT1(3-I)=(1.D0-2.D0*X+BKT1(I)**2)**0.5D0  
      ALMY(I)=X/BKT1(I) 
      ALMY(3-I)=(1.D0-X)/BKT1(3-I)
      XMN(3-I)=1.D0-XMN(I)      
   22 RETURN     
      END 
